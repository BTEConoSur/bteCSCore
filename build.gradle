plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("com.gradleup.shadow") version "9.0.0"
}

group = 'com.bteconosur'
version = '3.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven {
        name = "Smyler Releases"
        url = "https://maven.smyler.net/releases/"
    }

    maven { url = "https://repo.onarandombox.com/content/groups/public/" }
    maven { url = "https://maven.enginehub.org/repo/" }
    jcenter()
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    implementation("net.kyori:ansi:1.1.1")
    implementation("com.fasterxml.jackson.core:jackson-databind:2.15.2")

    compileOnly("net.buildtheearth.terraminusminus:terraminusminus-core:2.1.2-1.21.8")
    compileOnly("net.buildtheearth.terraminusminus:terraminusminus-bukkit:2.1.2-1.21.8")

    implementation("org.hibernate.orm:hibernate-core:7.1.6.Final")
    implementation("org.hibernate:hibernate-spatial:7.1.6.Final")
    implementation("org.reflections:reflections:0.10.2")
    implementation("org.mariadb.jdbc:mariadb-java-client:3.3.2")
    implementation("net.dv8tion:JDA:6.1.1")

    implementation("org.locationtech.jts:jts-core:1.20.0")
    implementation("org.locationtech.jts.io:jts-io-common:1.20.0")

    compileOnly("org.mvplugins.multiverse.core:multiverse-core:5.3.3-SNAPSHOT")
    implementation(platform("com.intellectualsites.bom:bom-newest:1.44")) // Ref: https://github.com/IntellectualSites/bom 
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Core")
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit")

    implementation("dev.triumphteam:triumph-gui:3.1.13")
    compileOnly("com.arcaniax:HeadDatabase-API:1.3.2")

    compileOnly("net.luckperms:api:5.4")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

shadowJar {
    archiveFileName = "${project.name}-${project.version}.jar"
    mergeServiceFiles()
    exclude 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/LICENSE*',
          'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/NOTICE*',
          'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    relocate("dev.triumphteam.gui", "com.bteconosur.core.gui")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('build') {
    dependsOn(tasks.named('shadowJar'))
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}